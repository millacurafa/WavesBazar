{-# STDLIB_VERSION 3 #-}
{-# CONTENT_TYPE DAPP #-}
{-# SCRIPT_TYPE ACCOUNT #-}


let NONE = "none"

func getNumberByKey(key: String) = {
    let num = match getInteger(this, key) {
        case a:Int => a 
        case _ => 0
    }
    num
}

func getStrByKey(key: String) = {
    let str = match getString(this, key) {
        case a:String => a 
        case _ => NONE
    }
    str
}

func getKeyItemPrice(item: String) = {
    item + "_price"
}

func getValueItemPrice(item:String) = {
    getNumberByKey(getKeyItemPrice(item))
}

func getKeyUserItemCounter(user: String, item: String) = {
    item + "_" + user + "_cnt"
}

func getValueUserItemCounter(user: String, item: String) = {
    getNumberByKey(getKeyUserItemCounter(user, item))
}

func getKeyItem(supplier: String, title: String) = {
    "item_" + toBase58String(sha256(toBytes(supplier + title)))
}
func getKeyItemData(item: String) = {
    item + "_data"
} 

func getKeyItemSupplier(item: String) = {
    item + "_owner"
}
func getValueItemSupplier(item: String) = {
    getStrByKey(getKeyItemSupplier(item))
}
func getKeyBalanceSupplier(account: String) = {
    account + "_balance"
}

func getValueBalanceSupplier(account: String) = {
    getNumberByKey(getKeyBalanceSupplier(account))
}



@Callable(i)
func addItem(title: String, price: Int, data: String) = {
    let supplierAddress = toBase58String(i.caller.bytes)
    let item = getKeyItem(supplierAddress, title)
    if ( price <= 0 ) then throw("amount lower than item price")
    else if ( getValueItemSupplier(item) != NONE ) then throw("an item already exists")
    else {
        WriteSet([
            DataEntry(getKeyItemSupplier(item), supplierAddress),
            DataEntry(getKeyItemPrice(item), price),
            DataEntry(getKeyItemData(item), data)  
        ])
    }
}

@Callable(i)
func purchase(item:String) ={
    let pmt = extract(i.payment)
    if (isDefined(pmt.assetId)) then throw("just Waves accepted")
    else{
        let userAddress = toBase58String(i.caller.bytes)
        let price = getValueItemPrice(item)
        let supplierAddress = getValueItemSupplier(item)
        if (pmt.amount < price) then throw("you are trying to pay less, don't be greedy")
        else if (pmt.amount > price) then throw("thanks for your kind donation, but let's pay just wat is needed")
        else if (supplierAddress == NONE) then throw("supplier not availabel in register")
        else {
            WriteSet([
            DataEntry(getKeyUserItemCounter(userAddress, item), getValueUserItemCounter(userAddress, item) +1),
            DataEntry(getKeyBalanceSupplier(supplierAddress), getValueBalanceSupplier(supplierAddress) + pmt.amount)
            ])
        }
    }
}

@Callable(i)
func withdraw() = {
    let supplierAddress = toBase58String(i.caller.bytes)
    let balance = getValueBalanceSupplier(supplierAddress)
    if (balance <= 0)
        then throw("you don't have enough balance")
        else ScriptResult(
            WriteSet([
            DataEntry(getKeyBalanceSupplier(supplierAddress), 0)
            ]),
            TransferSet([
            ScriptTransfer(
            addressFromStringValue(supplierAddress), balance, unit
            )])
        )

}


